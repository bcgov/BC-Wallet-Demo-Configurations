// Get an image's hash tag
String getImageTagHash(String imageName, String tag = "") {

  if(!tag?.trim()) {
    tag = "latest"
  }

  def istag = openshift.raw("get istag ${imageName}:${tag} -o template --template='{{.image.dockerImageReference}}'")
  return istag.out.tokenize('@')[1].trim()
}

node {
  stage("Init") {
    script {
      dir(''){
        checkout scm
        config = load "jenkins/config.groovy"
      }
    }
  }
  stage("Building images ...") {
    script {
      openshift.withCluster() {
        openshift.withProject() {

          // Find all of the build configurations associated to the application using labels ...
          def buildconfigs = openshift.selector("bc", [ app : "${config.APP_LABEL}" ])
          echo "Found ${buildconfigs.count()} buildconfigs for app label (app=${config.APP_LABEL}): ${buildconfigs.names()}"

          // Kick off all the builds in parallel ...
          def builds = buildconfigs.startBuild()
          echo "Started ${builds.count()} builds: ${builds.names()}"

          timeout(config.WAIT_TIMEOUT) {
            // Wait for all the builds to complete ...
            // This section will exit after the last build completes.
            echo "Waiting for builds to complete ..."
            builds.withEach {
              // untilEach and watch - do not support watching multiple named resources,
              // so we have to feed it one at a time.
              it.untilEach(1) {
                  echo "${it.object().status.phase} - ${it.name()}"
                  return (it.object().status.phase == "Complete")
              }
            }
          }

          echo "Builds complete ..."
        }
      }
    }
  }

  for(item in config.COMPONENTS)
  {
    stage("Deploying ${item}") {
      script {
        openshift.withCluster() {
          openshift.withProject() {

            echo "Tagging ${item} for deployment to ${config.DEV_ENV} ..."

            // Don't tag with BUILD_ID so the pruner can do it's job; it won't delete tagged images.
            // Tag the images for deployment based on the image's hash
            def IMAGE_HASH = getImageTagHash("${item}")
            echo "IMAGE_HASH: ${IMAGE_HASH}"
            openshift.tag("${item}@${IMAGE_HASH}", "${item}:${config.DEV_ENV}")
          }

          openshift.withProject("${config.NAME_SPACE}-${config.DEV_ENV}") {
              def dc = openshift.selector('dc', "${item}")
              // Wait for the deployment to complete.
              // This will wait until the desired replicas are all available
              dc.rollout().status()
          }

          echo "Deployment Complete."
        }
      }
    }
  }
}